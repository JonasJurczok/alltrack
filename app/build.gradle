apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "org.linesofcode.alltrack"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        showStandardStreams = true
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:23.0.1"
    compile "com.android.support:design:23.0.1"
    compile "com.android.support:recyclerview-v7:23.0.1"

    compile "com.j256.ormlite:ormlite-core:4.41"
    compile "com.j256.ormlite:ormlite-android:4.41"

    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test:runner:0.4') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

coveralls {
    jacocoReportPath = 'build/reports/coverage/debug/report.xml'
}

task grantAnimationPermission(type: Exec, dependsOn: 'installDebug') {
    commandLine "./adb_all.sh shell pm grant org.linesofcode.alltrack android.permission.SET_ANIMATION_SCALE".split(' ')
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('connected')) {
        task.dependsOn grantAnimationPermission
    }
}